archetype (adl_version=1.4; uid=aa128bc4-5da9-4e32-bf3d-4a2e0841939d)
	openEHR-EHR-OBSERVATION.body_part_examination.v0

concept
	[at0000]	-- Body Part Examination
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Bert Verhees">
		["date"] = <"2017-02-09">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the findings and interpretation of body part examination results performed on body parts like abdomen and thoracic. ">
			use = <"Use to record any body part examination result, like abdomen or thoracic, including the result of a test on a specimen taken as part of a composite procedure or operation.

Multi-analyte panels can be represented using templates or specialised archetypes.
 
Will normally be reported back to the requesting clinician as one component within the context of an overall COMPOSITION-based report.">
			keywords = <"transplantation", "body-part", "radiology", "haematology", "microbiology", "immunology", "laboratory", "anatomical", "chemical", "clinical", "immunopathology", "cytology", "test", "specimen", "forensic", "genetic">
			misuse = <"This archetype has been designed to support research in context of body structure transplantation and may not be appropriate for clinical pathology or other kinds of pathology use.">
			copyright = <"">
		>
	>
	lifecycle_state = <"NotSet">
	other_contributors = <"Laura Boogert", ...>
	other_details = <
		["revision"] = <"0.0.1-alpha">
		["original_publisher"] = <"openEHR Foundation">
		["build_uid"] = <"b123404b-6163-4105-a9ff-a123454eeafd">
		["original_namespace"] = <"org.openehr">
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["MD5-CAM-1.0.1"] = <"EA7B99F3C48753AEBC945335A35EA653">
		["custodian_namespace"] = <"org.openehr">
		["custodian_organisation"] = <"openEHR Foundation">
	>

definition
	OBSERVATION[at0000] matches {	-- Body Part Examination
		data matches {
			HISTORY[at0001] matches {	-- Event Series
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..*} matches {	-- Any event
						data matches {
							ITEM_TREE[at0003] matches {	-- Tree
								items cardinality matches {1..*; unordered} matches {
									ELEMENT[at0005] occurrences matches {0..1} matches {	-- Test name
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0073] occurrences matches {0..1} matches {	-- Test status
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0107, 	-- Registered
													at0037, 	-- Partial
													at0038, 	-- Final
													at0115, 	-- Corrected
													at0040, 	-- Amended
													at0074, 	-- Cancelled
													at0116]	-- Entered in error
												}
											}
										}
									}
									ELEMENT[at0075] occurrences matches {0..1} matches {	-- Test status timestamp
										value matches {
											DV_DATE_TIME matches {*}
										}
									}
									ELEMENT[at0077] occurrences matches {0..1} matches {	-- Diagnostic service category
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0100] occurrences matches {0..1} matches {	-- Clinical information provided
										value matches {
											DV_TEXT matches {*}
										}
									}
									allow_archetype CLUSTER[at0097] occurrences matches {0..*} matches {	-- Test findings
										include
											archetype_id/value matches {/.*/}
									}
								}
							}
						}
						state matches {
							ITEM_TREE[at0112] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0113] occurrences matches {0..*} matches {	-- Confounding factors
										value matches {
											DV_TEXT matches {*}
										}
									}
									allow_archetype CLUSTER[at0114] occurrences matches {0..*} matches {	-- Patient state details
										include
											archetype_id/value matches {/.*/}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0004] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0017] occurrences matches {0..1} matches {	-- Responsible hospital
						include
							archetype_id/value matches {/.*/}
					}
					CLUSTER[at0094] occurrences matches {0..*} matches {	-- Test request details
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0106] occurrences matches {0..*} matches {	-- Original test requested Name
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0062] occurrences matches {0..1} matches {	-- Requester order identifier
								value matches {
									DV_IDENTIFIER matches {*}
								}
							}
							ELEMENT[at0063] occurrences matches {0..1} matches {	-- Receiver order identifier
								value matches {
									DV_IDENTIFIER matches {*}
								}
							}
							allow_archetype CLUSTER[at0090] occurrences matches {0..1} matches {	-- Requester
								include
									archetype_id/value matches {/.*/}
							}
							allow_archetype CLUSTER[at0035] occurrences matches {0..*} matches {	-- Distribution list
								include
									archetype_id/value matches {/openEHR-EHR-CLUSTER\.distribution(-[a-zA-Z0-9_]+)*\.v1/}
							}
						}
					}
					allow_archetype CLUSTER[at0110] occurrences matches {0..*} matches {	-- Test method/device details
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.device(-[a-zA-Z0-9_]+)*\.v1/}
					}
					allow_archetype CLUSTER[at0117] occurrences matches {0..*} matches {	-- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Body Part Examination">
					description = <"The findings and interpretation of a Body Part Examination.">
					comment = <"This archetype may be used to record a single valued test, but will often be specialised or templated to represent multiple value or 'panel' tests. This archetype also acts as the parent for specialisations appropriate for more specific laboratory tests, e.g. microbiology, histopathology.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0005"] = <
					text = <"Test name">
					description = <"Identification of the pathology test performed, sometimes including specimen type and patient state.">
					comment = <"A test result may be for a single analyte, or a group of items, including panel tests. Coding with a terminology, potientially a pre-coordinated term including specimen type, is preferred, where possible. May be coded with LOINC or Snomed-CT. Examples include \"Glucose\", \"Urea and Electrolytes\", \"Swab\",  “Cortisol (am)” or \"Liver Biopsy\".">
					fhirmapping = <"Diagnostic.Reportname">
					fhirvalueset = <"reportnames">
				>
				["at0017"] = <
					text = <"Responsible hospital">
					description = <"Demographic details of the laboratory with responsibility for the test.">
					fhir_mapping = <"DiagnosticReport.performer">
				>
				["at0035"] = <
					text = <"Distribution list">
					description = <"Details of a clinician or organisation who will receive a copy of the laboratory test result.">
				>
				["at0037"] = <
					text = <"Partial">
					description = <"This is a partial (e.g. initial, interim or preliminary) Test Result: data in the Test Result may be incomplete or unverified.">
				>
				["at0038"] = <
					text = <"Final">
					description = <"The Test result is complete and verified by an authorised person.">
				>
				["at0040"] = <
					text = <"Amended">
					description = <"The result has been modified subsequent to being Final, and is complete and verified by the responsible pathologist, and result data has been changed.">
				>
				["at0062"] = <
					text = <"Requester order identifier">
					description = <"The local ID assigned to the test order by the order requester.">
					comment = <"Equivalent to the HL7 Placer Order Identifier.">
				>
				["at0063"] = <
					text = <"Receiver order identifier">
					description = <"The local ID assigned to the test order by the order filler, usually by the Laboratory Information System (LIS).">
					comment = <"Assigning an identifier to a request by the Laboratory lnformation System (LIS) enables tracking progress of the request and enables linking results to requests. It also provides a reference to assist with enquiries and it is usually equivalent to the HL7 Filler Order Identifier.">
				>
				["at0073"] = <
					text = <"Test status">
					description = <"The status of the laboratory test result as a whole.">
					fhir_mapping = <"DiagnosticReport.status">
				>
				["at0074"] = <
					text = <"Cancelled">
					description = <"The result is unavailable because the test was not started or not completed (also sometimes called 'aborted').">
				>
				["at0075"] = <
					text = <"Test status timestamp">
					description = <"The date and/or time that the entire result was issued for the recorded ‘Test Report Status’.">
					fhir_mapping = <"DiagnosticReport.issued">
				>
				["at0077"] = <
					text = <"Diagnostic service category">
					description = <"The diagnostic service that performs the examination.">
					comment = <"For example, biochemistry, haematology. This helps clinicians filter for broad categories of results accoridng to local laboratory arrangements. Coding with a terminology is desirable, where possible.
">
					fhir_mapping = <"DiagnosticReport.serviceCategory">
					fhir_valueset = <"diagnostic-service-sections">
				>
				["at0090"] = <
					text = <"Requester">
					description = <"Details of the clinician or organisation requesting the laboratory test result.">
				>
				["at0094"] = <
					text = <"Test request details">
					description = <"Details concerning a single laboratory test requested.">
					comment = <"Usually there is one test request (or \"order\") for each result, however in some circumstances multiple test requests may be represented using a single Pathology test result archetype.">
					fhir_mapping = <"DiagnosticReport.requestDetail">
				>
				["at0097"] = <
					text = <"Test findings">
					description = <"Details about the  reported results, possibly an individual analyte, a group, panel or battery of multiple results, or a more complex and specific structure.">
					fhir_mapping = <"DiagnosticReport.result">
				>
				["at0100"] = <
					text = <"Clinical information provided">
					description = <"Description of clinical information available at the time of interpretation of results, which may include a link to the original clinical information provided in the test request.">
					comment = <"In HL7 fhir this information is held within the linked requestDetail.clinical Notes.">
					fhir_mapping = <"DiagnosticReport.requestDetail.clinicalNotes">
				>
				["at0106"] = <
					text = <"Original test requested Name">
					description = <"Identification of the original laboratory test requested, where the test requested differs from the test actually performed.">
				>
				["at0107"] = <
					text = <"Registered">
					description = <"The existence of the test is registered in the Laboratory Information System, but there is nothing yet available.">
				>
				["at0110"] = <
					text = <"Test method/device details">
					description = <"Additional structured details of laboratory test methodology followed. For example, structured details about the laboratory method and data interpretation used.">
				>
				["at0112"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0113"] = <
					text = <"Confounding factors">
					description = <"Aspects of the condition or circumstances of the patient that may alter interpretation of the laboratory test.">
				>
				["at0114"] = <
					text = <"Patient state details">
					description = <"Details of patient state that may alter interpretation of the laboratory test.">
				>
				["at0115"] = <
					text = <"Corrected">
					description = <"The result has been modified subsequent to being Final, and is complete and verified by the responsible pathologist.">
				>
				["at0116"] = <
					text = <"Entered in error">
					description = <"The Test Result has been withdrawn following previous Final release.">
				>
				["at0117"] = <
					text = <"Extension">
					description = <"Slot to allow extension to support localisation or alignment with other standards/ reference models.">
				>
			>
		>
	>
